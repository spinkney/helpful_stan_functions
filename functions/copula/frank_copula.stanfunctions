/** @addtogroup frank Frank Copula Functions
 * The Frank copula bivariate cumulative density function is defined as 
 * \f[
 *      C(u,v) = -\frac{1}{\omega}\ln \big[1 + \frac{(\exp(-\omega u) - 1)(\exp(-\omega v)-1)}{\exp(-\omega) -1} \big]
 * \f]
 * for \f$\omega \in (-\infty, \infty) \,/\ \{0\}\f$. 
 * 
 *  The Frank copula bivariate probability density function is defined as
 * \f[
 *     c(u, v) = \frac{ \partial^2 C(u, v) }{ \partial u \partial v} = \frac{\omega (1 - \exp(-\omega))(\exp(-\omega(u + v)))}{[ (1 - \exp(-\omega)) - (1 - \exp(-\omega u))(1 - \exp(-\omega v))]^2}
 * \f]
 *
 * @include \copula\frank_copula.stanfunctions
 *
 * \ingroup copula
 *  @{ */

/** 
 * Frank Bivariate Copula Log Density
 *
 * Sean Pinkney \n
 *
 * The copula is defined \f$\omega \in (-\infty, \infty) \,/\ \{0\}\f$
 *
 * @param u Vector with elements in \f$(0,1]\f$, not checked but function will return NaN
 * @param v Vector with elements in \f$(0,1]\f$, not checked but function will return NaN
 * @param \omega Real number on (-Inf, Inf) / {0}, values of \omega greater than 37 will result in overflow
 * @return log density
 */
real frank_copula_lpdf(vector u, vector v, real theta) {
  int N = num_elements(u);
  real log_lik;
  
  if (theta < 0) 
    log_lik = N * log(theta * (1 - exp(-theta))) - theta * sum(append_col(u, v))
              - sum(
                    log(
                        (-expm1(-theta) - (expm1(-theta * u)) .* (expm1(-theta * v))) ^ 2));
  else 
    log_lik = N * (log(theta) + log1m_exp(-theta)) - theta * sum(append_col(u, v))
              - 2 * sum(log(-expm1(-theta) - (expm1(-theta * u)) .* (expm1(-theta * v))));
  
  return log_lik;
}

/** @} */